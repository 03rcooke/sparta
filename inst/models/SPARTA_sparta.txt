#######################################
# SPARTA model from GitHub 26/05/2016 #
#######################################

# Priors 
# State model priors
for(t in 1:nyear){
  a[t] ~ dunif(-10,10)   
}                 

# RANDOM EFFECT for SITE
for (i in 1:nsite) {
  eta[i] ~ dnorm(0, tau2)       
} 

tau2 <- 1/(sigma2 * sigma2) 
sigma2 ~ dunif(0, 5)


# Observation model priors 
for (t in 1:nyear) {
  alpha.p[t] ~ dnorm(mu.lp, tau.lp)            
}

mu.lp ~ dnorm(0, 0.01)                         
tau.lp <- 1 / (sd.lp * sd.lp)                 
sd.lp ~ dunif(0, 5)   

# State model
for (i in 1:nsite){ 
  for (t in 1:nyear){   
    z[i,t] ~ dbern(muZ[i,t]) # True occupancy z at site i
    logit(muZ[i,t])<- a[t] + eta[i] # plus random site effect
  }}   

# Derived parameters state model

# Finite sample occupancy
for (t in 1:nyear) {  
  psi.fs[t] <- sum(z[1:nsite,t])/nsite
} 

# Overall trend in occupuancy
sumY <- sum(psi.fs[1:nyear])
for (t in 1:nyear) {
  sumxy[t] <- psi.fs[t]*t
}
sumXY <- sum(sumxy[1:nyear])
regres.psi <- (sumXY - ((sumX*sumY)/nyear))/(sumX2 - ((sumX*sumX)/nyear))

# Derived parameters observation model
for (t in 1:nyear) {          
  pdet.alpha[t] <- exp(alpha.p[t])/(1 + exp(alpha.p[t])) 
}

# overall trend in pdet.alpha
sumYpdet <- sum(pdet.alpha[1:nyear])  
for (t in 1:nyear) {          
  sumxypdet[t] <- pdet.alpha[t]*t
}
sumXYpdet <- sum(sumxypdet[1:nyear])
regres.pdet <- (sumXYpdet - ((sumX*sumYpdet)/nyear))/(sumX2 - ((sumX*sumX)/nyear))
